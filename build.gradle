buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'edu.sc.seis.macAppBundle' version '2.3.0'
    id 'edu.sc.seis.launch4j' version '2.4.4'
}

group 'USB-webcam'
version '0.7.2'

repositories {
    mavenCentral()
}

configurations.all {
    exclude group: "commons-logging", module: "commons-logging"
}

dependencies {
    // COM port lib
    compile "org.scream3r:jssc:2.8.0"

    // webcam
    compile "com.github.sarxos:webcam-capture:0.3.12"

    // log
    compile 'org.slf4j:jcl-over-slf4j:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.2.3'
}

sourceCompatibility = 1.8

shadowJar {
    minimize()
    archiveName = "Usb_Webcam-${version}.${extension}"
    destinationDir = file("build/distributions")
}

macAppBundle {
    mainClassName = 'com.pony101.Main'
}

launch4j {
    mainClassName = 'com.pony101.Main'
    jar = "../distributions/${shadowJar.archiveName}"
    outputDir = "windows"
    xmlFileName = "launch.xml"
}